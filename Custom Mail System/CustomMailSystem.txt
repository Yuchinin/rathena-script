//===== Reveria Chronicle Scripts =======================================
//= Custom Mail System
//===== By: ==================================================
//= Yuchinin
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A custom mail system use in Reveria Chronicle Project
//=====******** Note ********=================================
//= This script need my release of:
//= 1. Common Useful Function	(common.txt)
//= Better let it load first before adding my other script.
//===== Additional Comments: =================================
//= 1.0 Initial release.
//= 1.1 Add auto table installation
//= 1.2 Optimize the mailopen function
//= 1.3 Add delete all read mail selection and mail limit
//= 1.4 Mail Limit Optimize
//= 1.5 More User Friendly
//===== NOTE: ================================================
//= Usable function:
//= 1. 	mailadd(<"sender">,<"dest">,<"title">,<"msg">,<"variable">,<"iteminfo">)
//=    	Example mailadd("GM01","GM02","I am title","Reward 100z and 10 #CASHPOINTS, additional 2 red potion and 3 orange potion","Zeny:100|#CASHPOINTS:10");
//= 2. 	mailopen()
//= 	Will open custom mailbox
//============================================================

function	script	maildelete	{
	if( getarg(0) < 0 ){
		query_sql("delete from mail_custom where dest_id = "+getcharid(0)+" AND status = 1");
	}else{
		query_sql("delete from mail_custom where id = "+getarg(0));
	}
	return;
}

function	script	mailgetdata	{
	deletearray @id,getarraysize(@id);
	deletearray @sender$,getarraysize(@sender$);
	deletearray @title$,getarraysize(@title$);
	deletearray @msg$,getarraysize(@msg$);
	deletearray @date$,getarraysize(@date$);
	deletearray @status,getarraysize(@status);
	deletearray @variable$,getarraysize(@variable$);
	deletearray @iteminfo$,getarraysize(@iteminfo$);
	.@size = query_sql("select id, send_name, title, message, date, status, variable, iteminfo from mail_custom where dest_id = "+getcharid(0)+" AND status = "+getarg(0)+" order by date desc",@id, @sender$, @title$, @msg$, @date$, @status, @variable$, @iteminfo$);
	if(.@size<0) .@size = 0;
	//dispbottom "maillgetdata size = "+.@size;
	return .@size;
}

function	script	mailsetstatus	{
	query_sql("update mail_custom set status = "+getarg(1)+" where id = "+getarg(0));
	return;
}

function	script	maildetail	{
	@size = mailgetdata(getarg(0));
	if(@size<=0) return;
	@m$ = "";
	for(@x=0;@x<@size;@x++){
		@m$ = @m$ + (@x+1)+". "+"["+(@status[@x]?CStr("o","green"):CStr("x","red"))+"]"+" [ "+@sender$[@x]+" ] [ "+@title$[@x]+" ]:";
	}
	next;
	@s = select(@m$);
	@s--;
	mes "[ "+@title$[@s]+" ]","_____________________________",@msg$[@s],"_____________________________",@date$[@s],"_____________________________";
	deletearray @T$,getarraysize(@T$);
	explode(@T$,@variable$[@s],"|");
	for(@x=0;@x<getarraysize(@T$);@x++){
		explode(@var$,@T$[@x],":");
		mes @var$[0]+": "+@var$[1];
		if(!@status[@s]) set getd(@var$[0]),getd(@var$[0])+atoi(@var$[1]);
	}
	deletearray @T$,getarraysize(@T$);
	explode(@T$,@iteminfo$[@s],"|");
	for(@x=0;@x<getarraysize(@T$);@x++){
		explode(@var$,@T$[@x],":");
		mes @var$[1]+" x "+Itemlink(atoi(@var$[0]));
		if(!@status[@s]) getitem atoi(@var$[0]),atoi(@var$[1]);
	}
	mes "_____________________________";
	if(!@status[@s]) {
		mes CStr("Mark as read.","red");
		mailsetstatus(@id[@s],1);
	}
	//next;
	@ss = select("Back",CStr("Delete Mail","red"),"Back to Main");
	if(@ss==1){
		return 1;
	}else if(@ss==2){
		maildelete(@id[@s]);
		mes CStr("Mail Deleted.");
		return 1;
	}else if(@ss==3){
		return 2;
	}
	return 0;
}

function	script	getcharidwithname	{
	query_sql("select char_id from `char` where name = '"+getarg(0)+"'",.@charid);
	return .@charid;
}

// Don't touch function above !!!!!!!!!!!!!!!!!!!!!! ==============================================================

//==========================
//= mailadd(<"sender">,<"dest">,<"title">,<"msg">,<"variable">,<"iteminfo">{,<boolean>})	// booleon = 1 delete item on successful send
//= Example mailadd("GM01","GM02","I am title","Reward 100z and 10 #CASHPOINTS, additional 2 red potion and 3 orange potion","Zeny:100|#CASHPOINTS:10");
//= return 1 on success
//= return 0 when fail and will output error message on server
//==========================
function	script	mailadd	{
	// Setting Part ==============
	.@MaxItem = 6;
	.@MaxVariable = 2;
	.@MaxMail = 100;
	// ===========================
	.@sender$ = getarg(0,"");
	.@senderid = getcharidwithname(.@sender$);
	.@receiver$ = getarg(1,"");
	.@receiverid = getcharidwithname(.@receiver$);
	.@title$ = getarg(2,"No Title");
	.@msg$ = getarg(3,"No Message");
	.@variable$ = getarg(4,"");
	.@iteminfo$ = getarg(5,"");
	.@delitem = getarg(6,0);
	// Value Checking ============
/*	Npc don't have charid
	if(!.@senderid){
		debugmes "addmail: No sender name "+.@sender$;
		return 0;
	}
*/
	if(!.@receiverid){
		debugmes "addmail: No receiver name "+.@receiver$;
		mes CStr("Note","red")+" No receiver name "+.@receiver$;
		return 0;
	}
	deletearray .@T$,getarraysize(.@T$);
	explode(.@T$,.@iteminfo$,"|");
	if(getarraysize(.@T$) > .@MaxItem){
		debugmes "addmail: iteminfo error, Max item allow is "+.@MaxItem;
		mes CStr("Note","red")+" Max item allow is "+.@MaxItem;
		return 0;
	}
	deletearray .@T$,getarraysize(.@T$);
	explode(.@T$,.@variable$,"|");
	if(getarraysize(.@T$) > .@MaxVariable){
		debugmes "addmail: variable error, Max variable allow is "+.@MaxVariable;
		mes CStr("Note","red")+" Max variable allow is "+.@MaxItem;
		return 0;
	}
	query_sql("select count(id) as count from mail_custom where send_id = "+.@senderid,.@count);
	if(.@count >= .@MaxMail){
		debugmes "addmail: Mailbox full, current max is "+.@MaxMail;
		mes CStr("Note","red")+" Mailbox full, current max is "+.@MaxMail;
		return 0;
	}
	.@sql$ = "insert into mail_custom set send_name = '"+.@sender$+"', send_id = "+.@senderid+", dest_name = '"+.@receiver$+"', dest_id = "+.@receiverid+", title = '"+.@title$+"', message = '"+.@msg$+"', variable = '"+.@variable$+"', iteminfo = '"+.@iteminfo$+"'";
	query_sql(.@sql$);
	query_sql("select count(id) as count from mail_custom where send_id = "+.@senderid,.@count2);
	if(.@count+1 == .@count2){
		message .@receiver$,"You have a new mail.";
		emotion e_mail,1,.@receiver$;
		if(.@delitem){
			explode(.@T$,.@iteminfo$,"|");
			for(.@x=0;.@x<getarraysize(.@T$);.@x++){
				explode(.@TT$,.@T$[.@x],":");
				delitem atoi(.@TT$[0]),atoi(.@TT$[1]);
			}
		}
		return 1;
	}else{
		debugmes "addmail: Unknown Error occur! Please contact developer!";
		return 0;
	}
}

//==========================
//= mailopen()
//= Will open custom mailbox
//==========================
function	script	mailopen	{
MAIN:
	mes CStr("[ Reveria MailBox System ]","red"),"Welcome adventurer, this is Reveria Chronicles MailBox Service.";
	next;
	@s = select("Unread ["+CStr(mailgetdata(0),"red")+"] "+(mailgetdata(0)?CStr("New","red"):""), "Read ["+CStr(mailgetdata(1),"red")+"]", "Delete All Read ["+CStr(mailgetdata(1),"red")+"]");
	switch(@s){
		case 1:
			do{
				mes CStr("[ Reveria MailBox System ]","red"),"Unread Mail List:";
				@s = maildetail(0);
				if(@s==2) break;
				if(mailgetdata(0)<=0){
					mes "You don't have any unread mail.";
					break;
				}
				next;
			}while(mailgetdata(0)>0);
			break;
		case 2:
			do{
				mes CStr("[ Reveria MailBox System ]","red"),"Read Mail List:";
				@s = maildetail(1);
				if(@s==2) break;
				if(mailgetdata(1)<=0){
					mes "You don't have any read mail.";
					break;
				}
				next;
			}while(mailgetdata(1)>0);
			break;
		case 3:
			mes CStr("[ Reveria MailBox System ]","red"),"Confirm delete all read mail?";
			if(mailgetdata(1)<=0){
				mes "You don't have any read mail.";
				break;
			}
			if(select("No",CStr("Yes","red"))==2){
				maildelete(-1);
				mes "All read mail deleted.";
				break;
			}
			break;
		default:
			break;
	}
	next;
	goto MAIN;
}

izlude,119,145,4	script	MailTest	119,{
	mailadd(strnpcinfo(0),strcharinfo(0),"I am title","Reward 100z and 10 #CASHPOINTS, additional 2 red potion and 3 orange potion","Zeny:100|#CASHPOINTS:10","501:2|502:3");
OnInit:
waitingroom "Click to test mail",0;
}

-	script	MailBox::cmail	-1,{
// Thinking of replacing official openmail to this (need idea)
	mailopen();
	end;

OnInit:
.@sql$ = "CREATE TABLE IF NOT EXISTS `mail_custom` (`id` bigint(20) unsigned NOT NULL AUTO_INCREMENT,`send_name` varchar(30) NOT NULL DEFAULT '',`send_id` int(11) unsigned NOT NULL DEFAULT '0',`dest_name` varchar(30) NOT NULL DEFAULT '',`dest_id` int(11) unsigned NOT NULL DEFAULT '0',`title` varchar(45) NOT NULL DEFAULT '',`message` varchar(255) NOT NULL DEFAULT '',`date` timestamp NOT NULL DEFAULT CURRENT_TIMESTAMP,`status` tinyint(2) NOT NULL DEFAULT '0',`variable` varchar(100) NOT NULL DEFAULT '',`iteminfo` varchar(100) NOT NULL DEFAULT '',PRIMARY KEY (`id`)) ENGINE=MyISAM AUTO_INCREMENT=0 DEFAULT CHARSET=latin1";
query_sql(.@sql$);
}

izlude,119,140,4	duplicate(cmail)	Mailbox::cmailiz	888